<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="FB_ABL_185_Pnp" Id="{dac48496-9ab0-4457-8192-6bdc7e5f1cd2}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ABL_185_Pnp
(****************************************************************************************)
(*	TITEL				laden kristall- Modul	    										*)
(*	Status				ungetestet                         								*)
(*	Beschrieb:			Ablaufbeschreibung												*)
(*																						*)
(*	Steps:	self-explained using enum													*)
(***********************************Revisionen*******************************************)
(*	see github blames																	*)
(****************************************************************************************)

VAR_INPUT
    xIN_Reset       : BOOL;          // Schrittkette zurücksetzen 
    xIN_Init        : BOOL;          // Initialisieren
    xIN_Quit        : BOOL;          // Fehler quittieren 
    xIN_Start       : BOOL;          // Startbefehl
    xIN_Sperre      : BOOL;          // FB Sperren 
    xIN_NextStep    : BOOL;          // Nächsten Schritt ausführen 
    xIN_AlarmAktiv  : BOOL;          // Anlage hat Alarm
    xIN_DryCycle    : BOOL;          // Fahren ohne Teile
    xIN_RestartCYCL : BOOL;          // Zyklus erneut starten im STEP: END: LEERFAHREN
    enIN_State      : enState;       // Status Anlage
    tIN_Timeout     : TIME := T#10s; // Timout

    pxFG_Freigabe : POINTER TO BOOL; //Freigabe von Station

    // Application specific
    Mover              : REFERENCE TO FB_Mover;
    xIN_PceReadyToPick : BOOL; // the piece(s) is/are ready to picked
END_VAR
VAR_OUTPUT
    (* standart Ausgänge *)
    xOUT_ErrFlag  : BOOL;        // Error Flag 
    xOUT_Busy     : BOOL;        // Baustein befindet sich im Ablauf 
    xOUT_Done     : BOOL;        // Baustein hat den Ablauf beendet 		
    xOUT_Ready    : BOOL;        // Ready to begin: should be the same state as done	
    xOUT_Timeout  : BOOL;        // Timeout FB 
    xOUT_InitDone : BOOL;        // initialisierung fertig
    xOUT_Wait     : BOOL;        // Wartet auf Bedingung
    iOUT_Step     : ST185_Pnp_e; // Aktueller Schritt 
    sOUT_Step     : STRING;      // Aktueller Schritt but in string format
    bOUT_Error    : BYTE;        // Fehlernummer

    iOUT_ProcessCompletion : INT;  //process completion: express in % (0-100)
    xOUT_MoverFree         : BOOL; // The mover is free to move

END_VAR
VAR
    (* Standard *)
    (* Schritt *)
    iLastStep : ST185_Pnp_e; // Merker letzter Schritt 
    iPreStep  : ST185_Pnp_e; // Schritt zurück nach Alarm 
    iAktStep  : ST185_Pnp_e; // aktueller Schritt

    // Trigger Starten 
    rtSTART : R_TRIG;
    // Timer TimeOut 
    TON_Timeout : TON;

    (* Bausteine *)

    (* Spezial *)
    fb_axis185_01Y : FB_AxisLM_V1_1;
    fb_axis185_02Z : FB_AxisLM_V1_1;
    fb_axis185_10Z : FB_AxisLM_V1_1;

    fb_PnpHorAxis    : FB_LinearMotionMan;
    fb_PnpVerAxis_02 : FB_LinearMotionMan;
    fb_PnpVerAxis_10 : FB_LinearMotionMan;

    fb_simu_axis185_01Y : FB_Simu_AxisLM_V1_1;
    fb_simu_axis185_02Z : FB_Simu_AxisLM_V1_1;
    fb_simu_axis185_10Z : FB_Simu_AxisLM_V1_1;

    fb_griperLeft    : FB_ZylMo2El_V2_1;
    fb_griperRight   : FB_ZylMo2El_V2_1;
    simu_griperLeft  : FB_Simu_Zyl2Pos;
    simu_griperRight : FB_Simu_Zyl2Pos;
    fb_clampLeft     : FB_ZylMo2El_V2_1;
    fb_clampRight    : FB_ZylMo2El_V2_1;
    simu_clampLeft   : FB_Simu_Zyl2Pos;
    simu_clampRight  : FB_Simu_Zyl2Pos;

END_VAR
VAR CONSTANT
    PNP_SIMULATION : BOOL := FALSE; // Set to TRUE to simulate this part of the program
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*******************************************************************)
(* Trigger  *)
(*******************************************************************)
(* Zurücksetzen *)
IF xIN_RESET THEN
    iPreStep  := ST185_Pnp_e.RESET;
    iOUT_Step := ST185_Pnp_e.RESET;
END_IF;

(* Error Quittieren *)
IF (bOUT_Error <> 0) AND xIN_Quit THEN
    xOUT_ErrFlag := FALSE;
    bOUT_Error   := 0;
    iOUT_Step    := iPreStep;
END_IF;

(*******************************************************************)
(* Timer *)
(*******************************************************************)
TON_Timeout(IN := xIN_NextStep AND (iLastStep = iOUT_Step) (*todo FG *) ,
            PT := tIN_Timeout);

(*******************************************************************)
(* Bausteine *)
(*******************************************************************)
fb_PnpHorAxis(config          := _20_AUTO.recipe.linearAxis[LinearAxis_e.ST185_01Y],
              xIN_Init        := enIN_State = enState.StateINIT,
              xIN_Reset       := xIN_Reset,
              lrIN_CurrPos    := fb_axis185_01Y.rOUT_ActPos,
              lrIN_Delta_mm   := 0.5,
              iIN_CurrentStep := iOUT_Step,
              iIN_moveNumber  := ST185_PnpHorPos_e._max);

fb_simu_axis185_01Y(xIN_Enable          := PNP_SIMULATION AND CONST.USE_SIMULATION,
                    bIN_AxisCntr        := fb_axis185_01Y.bOUT_AxisCntr,
                    wIN_AxisState       := ST185_Axis.axis185_01Y.wState,
                    udiIN_HomingTime_ms := 2000,
                    pIN_axisSimu        := ADR(SIMU.axis_ref[Axis_e.ST185_01Y]),
                    pIN_axisHW          := ADR(ST185_Axis.axis185_01Y.axis),
                    rIN_PosActual       := ST185_Axis.axis185_01Y.rActPos,
                    rIN_PosMax          := ST185_Axis.const185_01Y.POS_MAX,
                    rIN_PosMin          := ST185_Axis.const185_01Y.POS_MIN,
                    rIN_PixMax          := 100,
                    rIN_PixMin          := 0);

fb_axis185_01Y(enIN_State       := enIN_State,
               parIN_Buttons    := ADR(AXIS.btTeachArAxis[Axis_e.ST185_01Y]),
               xIN_DisButtons   := FALSE,
               xIN_Quit         := xIN_Quit,
               xIN_PARAMok      := TRUE, (*TODO*)
               xIN_FREE         := TRUE,
               xIN_FREE_HOME    := TRUE,
               rIN_POSMIN       := ST185_Axis.const185_01Y.POS_MIN,
               rIN_POSMAX       := ST185_Axis.const185_01Y.POS_MAX,
               rIN_VELOMAX      := ST185_Axis.const185_01Y.VELO_MAX,
               rIN_VELOMANU     := ST185_Axis.const185_01Y.VELO_MANU,
               rIN_VELOOPEN     := ST185_Axis.const185_01Y.VELO_MANU / 4,
               rIN_JogInc       := AXIS.rTeachIncAxis,
               xIN_Execute      := fb_PnpHorAxis.xOUT_Execute,
               xIN_Relative     := FALSE,
               xIN_STOP         := FALSE,
               xIN_OFF          := FALSE,
               rIN_Position     := TO_REAL(fb_PnpHorAxis.OUT_MoveProfile.lrPosition),
               rIN_Velocity     := TO_REAL(fb_PnpHorAxis.OUT_MoveProfile.lrVelocity),
               rIN_Acceleration := TO_REAL(fb_PnpHorAxis.OUT_MoveProfile.lrAccel),
               rIN_Deceleration := TO_REAL(fb_PnpHorAxis.OUT_MoveProfile.lrDecel),
               rIN_Jerk         := TO_REAL(fb_PnpHorAxis.OUT_MoveProfile.lrJerk),
               rIN_PosManu      := TO_REAL(_20_AUTO.recipe.linearAxis[LinearAxis_e.ST185_01Y]
                                               .movesList[AXIS.bTeachPos[Axis_e.ST185_01Y]].lrPosition),
               rIN_VeloJogManu := TO_REAL(Axis.rJogVelo_pc),
               xIN_Home        := ST185_Pnp_e.Init_HomeHorizontal = iOUT_Step,
               xIN_Reset       := (ST185_Pnp_e.RESET = iOUT_Step) OR xIN_Reset,
               // The simu fb gives the hardware state when we are not in simu
               wAxisState    := fb_simu_axis185_01Y.wOUT_AxisState,
               bOUT_AxisCntr => st185_axis.axis185_01Y.bCtrlHi,
               rOUT_ActPos   => st185_axis.axis185_01Y.rActPos,
               axisIN_REF    := fb_simu_axis185_01Y.pOUT_axisRef^);

fb_PnpVerAxis_02(config          := _20_AUTO.recipe.linearAxis[LinearAxis_e.ST185_02Z],
                 xIN_Init        := enIN_State = enState.StateINIT,
                 xIN_Reset       := xIN_Reset,
                 lrIN_CurrPos    := fb_axis185_02Z.rOUT_ActPos,
                 lrIN_Delta_mm   := 1.8,
                 iIN_CurrentStep := iOUT_Step,
                 iIN_moveNumber  := ST185_PnpVerPos_e._max);

fb_simu_axis185_02Z(xIN_Enable          := PNP_SIMULATION AND CONST.USE_SIMULATION,
                    bIN_AxisCntr        := fb_axis185_02Z.bOUT_AxisCntr,
                    wIN_AxisState       := ST185_Axis.axis185_02Z.wState,
                    udiIN_HomingTime_ms := 2000,
                    pIN_axisSimu        := ADR(SIMU.axis_ref[Axis_e.ST185_02Z]),
                    pIN_axisHW          := ADR(ST185_Axis.axis185_02Z.axis),
                    rIN_PosActual       := ST185_Axis.axis185_02Z.rActPos,
                    rIN_PosMax          := ST185_Axis.const185_02Z.POS_MAX,
                    rIN_PosMin          := ST185_Axis.const185_02Z.POS_MIN,
                    rIN_PixMax          := 100,
                    rIN_PixMin          := 0);

fb_axis185_02Z(enIN_State       := enIN_State,
               parIN_Buttons    := ADR(AXIS.btTeachArAxis[Axis_e.ST185_02Z]),
               xIN_DisButtons   := FALSE,
               xIN_Quit         := xIN_Quit,
               xIN_PARAMok      := TRUE,
               xIN_FREE         := TRUE,
               xIN_FREE_HOME    := TRUE,
               rIN_POSMIN       := ST185_Axis.const185_02Z.POS_MIN,
               rIN_POSMAX       := ST185_Axis.const185_02Z.POS_MAX,
               rIN_VELOMAX      := ST185_Axis.const185_02Z.VELO_MAX,
               rIN_VELOMANU     := ST185_Axis.const185_02Z.VELO_MANU,
               rIN_VELOOPEN     := ST185_Axis.const185_02Z.POS_MAX / 4,
               rIN_JogInc       := AXIS.rTeachIncAxis,
               xIN_Execute      := fb_PnpVerAxis_02.xOUT_Execute,
               xIN_Relative     := FALSE,
               xIN_STOP         := FALSE,
               xIN_OFF          := FALSE,
               rIN_Position     := TO_REAL(fb_PnpVerAxis_02.OUT_MoveProfile.lrPosition),
               rIN_Velocity     := TO_REAL(fb_PnpVerAxis_02.OUT_MoveProfile.lrVelocity),
               rIN_Acceleration := TO_REAL(fb_PnpVerAxis_02.OUT_MoveProfile.lrAccel),
               rIN_Deceleration := TO_REAL(fb_PnpVerAxis_02.OUT_MoveProfile.lrDecel),
               rIN_Jerk         := TO_REAL(fb_PnpVerAxis_02.OUT_MoveProfile.lrJerk),
               rIN_PosManu      := TO_REAL(_20_AUTO.recipe.linearAxis[LinearAxis_e.ST185_02Z]
                                               .movesList[AXIS.bTeachPos[Axis_e.ST185_02Z]].lrPosition),
               rIN_VeloJogManu := TO_REAL(Axis.rJogVelo_pc),
               xIN_Home        := ST185_Pnp_e.Init_HomeVertical_02 = iOUT_Step,
               xIN_Reset       := (ST185_Pnp_e.RESET = iOUT_Step) OR xIN_Reset,
               wAxisState      := fb_simu_axis185_02Z.wOUT_AxisState,
               bOUT_AxisCntr   => ST185_Axis.axis185_02Z.bCtrlHi,
               rOUT_ActPos     => ST185_Axis.axis185_02Z.rActPos,
               axisIN_REF      := fb_simu_axis185_02Z.pOUT_axisRef^);

fb_PnpVerAxis_10(config          := _20_AUTO.recipe.linearAxis[LinearAxis_e.ST185_10Z],
                 xIN_Init        := enIN_State = enState.StateINIT,
                 xIN_Reset       := xIN_Reset,
                 lrIN_CurrPos    := fb_axis185_10Z.rOUT_ActPos,
                 lrIN_Delta_mm   := 1.0,
                 iIN_CurrentStep := iOUT_Step,
                 iIN_moveNumber  := ST185_PnpVerPos_e._max);

fb_simu_axis185_10Z(xIN_Enable          := PNP_SIMULATION AND CONST.USE_SIMULATION,
                    bIN_AxisCntr        := fb_axis185_10Z.bOUT_AxisCntr,
                    wIN_AxisState       := ST185_Axis.axis185_10Z.wState,
                    udiIN_HomingTime_ms := 2000,
                    pIN_axisSimu        := ADR(SIMU.axis_ref[Axis_e.ST185_10Z]),
                    pIN_axisHW          := ADR(ST185_Axis.axis185_10Z.axis),
                    rIN_PosActual       := ST185_Axis.axis185_10Z.rActPos,
                    rIN_PosMax          := ST185_Axis.const185_10Z.POS_MAX,
                    rIN_PosMin          := ST185_Axis.const185_10Z.POS_MIN,
                    rIN_PixMax          := 100,
                    rIN_PixMin          := 0);

fb_axis185_10Z(enIN_State       := enIN_State,
               parIN_Buttons    := ADR(AXIS.btTeachArAxis[Axis_e.ST185_10Z]),
               xIN_DisButtons   := FALSE,
               xIN_Quit         := xIN_Quit,
               xIN_PARAMok      := TRUE,
               xIN_FREE         := TRUE,
               xIN_FREE_HOME    := TRUE,
               rIN_POSMIN       := ST185_Axis.const185_10Z.POS_MIN,
               rIN_POSMAX       := ST185_Axis.const185_10Z.POS_MAX,
               rIN_VELOMAX      := ST185_Axis.const185_10Z.VELO_MAX,
               rIN_VELOMANU     := ST185_Axis.const185_10Z.VELO_MANU,
               rIN_VELOOPEN     := ST185_Axis.const185_10Z.POS_MAX / 4,
               rIN_JogInc       := AXIS.rTeachIncAxis,
               xIN_Execute      := fb_PnpVerAxis_10.xOUT_Execute,
               xIN_Relative     :=,
               xIN_STOP         :=,
               xIN_OFF          :=,
               rIN_Position     := TO_REAL(fb_PnpVerAxis_10.OUT_MoveProfile.lrPosition),
               rIN_Velocity     := TO_REAL(fb_PnpVerAxis_10.OUT_MoveProfile.lrVelocity),
               rIN_Acceleration := TO_REAL(fb_PnpVerAxis_10.OUT_MoveProfile.lrAccel),
               rIN_Deceleration := TO_REAL(fb_PnpVerAxis_10.OUT_MoveProfile.lrDecel),
               rIN_Jerk         := TO_REAL(fb_PnpVerAxis_10.OUT_MoveProfile.lrJerk),
               rIN_PosManu      := TO_REAL(_20_AUTO.recipe.linearAxis[LinearAxis_e.ST185_10Z]
                                               .movesList[AXIS.bTeachPos[Axis_e.ST185_10Z]].lrPosition),
               rIN_VeloJogManu := TO_REAL(Axis.rJogVelo_pc),
               xIN_Home        := ST185_Pnp_e.Init_HomeVertical_10 = iOUT_Step,
               xIN_Reset       := (ST185_Pnp_e.RESET = iOUT_Step) OR xIN_Reset,
               wAxisState      := fb_simu_axis185_10Z.wOUT_AxisState,
               bOUT_AxisCntr   => ST185_Axis.axis185_10Z.bCtrlHi,
               rOUT_ActPos     => ST185_Axis.axis185_10Z.rActPos,
               axisIN_REF      := fb_simu_axis185_10Z.pOUT_axisRef^);

//////////////////////////////////
fb_griperLeft(xIN_Reset    := xIN_Reset OR (ST185_Pnp_e.RESET = iOUT_Step),
              xIN_Quit     := xIN_Quit,
              xIN_Manu     := enState.StateMANU = enIN_State,
              pbtIN_Button := ADR(HMI.btManuCylinders[StationList_e.ST185][1]),
              xIN_GS       := simu_griperLeft.xOUT_inGS,
              xIN_AS       := simu_griperLeft.xOUT_inAS,
              xIN_FREE     := TRUE,
              xINtoGS      := ST185_Pnp_e.close_griper_pick_piece = iOUT_Step,
              xINtoAS      := _20_AUTO.recipe.st185.xUseVerLeft AND ST185_Pnp_e.open_griper_put_piece = iOUT_Step,
              tIN_ErrAS    := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                               [ST185_Cylinders_e.Greifer_Patrone_Links].toAS_alarm_time),
              tIN_ErrGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                            [ST185_Cylinders_e.Greifer_Patrone_Links].toGS_alarm_time),
              tIN_WaitAS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                             [ST185_Cylinders_e.Greifer_Patrone_Links].toAS_wait_time),
              tIN_WaitGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                             [ST185_Cylinders_e.Greifer_Patrone_Links].toGS_wait_time));

simu_griperLeft(xIN_Enable := PNP_SIMULATION AND CONST.USE_SIMULATION,
                xIN_inGS   := IO_BOX.I9200_ST185_greifer_patrone_links_zu,
                xIN_inAS   := IO_BOX.I9201_ST185_greifer_patrone_links_offen,
                xIN_valve  := fb_griperLeft.xOUT_VALVE,
                tIN_GS_ms  := 200,  //ms
                tIN_AS_ms  := 200); //ms

fb_griperRight(xIN_Reset    := xIN_Reset OR (ST185_Pnp_e.RESET = iOUT_Step),
               xIN_Quit     := xIN_Quit,
               xIN_Manu     := enState.StateMANU = enIN_State,
               pbtIN_Button := ADR(HMI.btManuCylinders[StationList_e.ST185][2]),
               xIN_GS       := simu_griperRight.xOUT_inGS,
               xIN_AS       := simu_griperRight.xOUT_inAS,
               xIN_FREE     := TRUE,
               xINtoGS      := ST185_Pnp_e.close_griper_pick_piece = iOUT_Step,
               xINtoAS      := _20_AUTO.recipe.st185.xUseVerRight AND ST185_Pnp_e.open_griper_put_piece = iOUT_Step,
               tIN_ErrAS    := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                                [ST185_Cylinders_e.Greifer_Patrone_Rechts].toAS_alarm_time),
               tIN_ErrGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                             [ST185_Cylinders_e.Greifer_Patrone_Rechts].toGS_alarm_time),
               tIN_WaitAS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                              [ST185_Cylinders_e.Greifer_Patrone_Rechts].toAS_wait_time),
               tIN_WaitGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                              [ST185_Cylinders_e.Greifer_Patrone_Rechts].toGS_wait_time));

simu_griperRight(xIN_Enable := PNP_SIMULATION AND CONST.USE_SIMULATION,
                 xIN_inGS   := IO_BOX.I9202_ST185_greifer_patrone_rechts_zu,
                 xIN_inAS   := IO_BOX.I9203_ST185_greifer_patrone_rechts_offen,
                 xIN_valve  := fb_griperRight.xOUT_VALVE,
                 tIN_GS_ms  := 200,  //ms
                 tIN_AS_ms  := 200); //ms

fb_clampLeft(xIN_Reset    := xIN_Reset OR (ST185_Pnp_e.RESET = iOUT_Step),
             xIN_Quit     := xIN_Quit,
             xIN_Manu     := enState.StateMANU = enIN_State,
             pbtIN_Button := ADR(HMI.btManuCylinders[StationList_e.ST185][3]),
             xIN_GS       := simu_clampLeft.xOUT_inGS,
             xIN_AS       := simu_clampLeft.xOUT_inAS,
             xIN_FREE     := TRUE,
             xINtoGS      := ST185_Pnp_e.clamp_up_to_rest_position = iOUT_Step,
             xINtoAS      := _20_AUTO.recipe.st185.xUseVerLeft AND
                             (ST185_Pnp_e.clamp_down_maintain_piece = iOUT_Step),
             tIN_ErrAS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                           [ST185_Cylinders_e.Niederhalter_Deckel_Links].toAS_alarm_time),
             tIN_ErrGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                           [ST185_Cylinders_e.Niederhalter_Deckel_Links].toGS_alarm_time),
             tIN_WaitAS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                            [ST185_Cylinders_e.Niederhalter_Deckel_Links].toAS_wait_time),
             tIN_WaitGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                            [ST185_Cylinders_e.Niederhalter_Deckel_Links].toGS_wait_time));

simu_clampLeft(xIN_Enable := PNP_SIMULATION AND CONST.USE_SIMULATION,
               xIN_inGS   := IO_BOX.I9204_ST185_niederhalter_deckel_links_oben,
               xIN_inAS   := NOT IO_BOX.I9204_ST185_niederhalter_deckel_links_oben,
               xIN_valve  := fb_clampLeft.xOUT_VALVE,
               tIN_GS_ms  := 200,  //ms
               tIN_AS_ms  := 200); //ms

fb_clampRight(xIN_Reset    := xIN_Reset OR (ST185_Pnp_e.RESET = iOUT_Step),
              xIN_Quit     := xIN_Quit,
              xIN_Manu     := enState.StateMANU = enIN_State,
              pbtIN_Button := ADR(HMI.btManuCylinders[StationList_e.ST185][4]),
              xIN_GS       := simu_clampRight.xOUT_inGS,
              xIN_AS       := simu_clampRight.xOUT_inAS,
              xIN_FREE     := TRUE,
              xINtoGS      := ST185_Pnp_e.clamp_up_to_rest_position = iOUT_Step,
              xINtoAS      := _20_AUTO.recipe.st185.xUseVerRight AND
                              (ST185_Pnp_e.clamp_down_maintain_piece = iOUT_Step),
              tIN_ErrAS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                            [ST185_Cylinders_e.Niederhalter_Deckel_Rechts].toAS_alarm_time),
              tIN_ErrGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                            [ST185_Cylinders_e.Niederhalter_Deckel_Rechts].toGS_alarm_time),
              tIN_WaitAS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                             [ST185_Cylinders_e.Niederhalter_Deckel_Rechts].toAS_wait_time),
              tIN_WaitGS := DINT_TO_TIME(_20_AUTO.recipe.cylinder2Pos[StationList_e.ST185]
                                             [ST185_Cylinders_e.Niederhalter_Deckel_Rechts].toGS_wait_time));

simu_clampRight(xIN_Enable := PNP_SIMULATION AND CONST.USE_SIMULATION,
                xIN_inGS   := IO_BOX.I9206_ST185_niederhalter_deckel_rechts_oben,
                xIN_inAS   := NOT IO_BOX.I9206_ST185_niederhalter_deckel_rechts_oben,
                xIN_valve  := fb_clampRight.xOUT_VALVE,
                tIN_GS_ms  := 200,  // ms
                tIN_AS_ms  := 200); // ms

(*******************************************************************)
(* OUTPUTS *)
(*******************************************************************)
// Error Flag 
xOUT_ErrFlag := (bOUT_Error <> 0);
// Baustein befindet sich im Ablauf 
xOUT_Busy := (iOUT_Step > ST185_Pnp_e.READY) AND (iOUT_Step < ST185_Pnp_e.DONE);
// Baustein hat den Ablauf beendet
xOUT_Done := (iOUT_Step = ST185_Pnp_e.DONE);
// Process ready
xOUT_Ready := (iOUT_Step = ST185_Pnp_e.READY) OR (iOUT_Step = ST185_Pnp_e.hor_move_to_extraction_pipe);
// Timeout FB 				 		
xOUT_Timeout := TON_Timeout.Q;
// Itialisierung fertig			
xOUT_InitDone := (iOUT_Step = ST185_Pnp_e.READY) OR
                 (iOUT_Step = ST185_Pnp_e.hor_move_to_extraction_pipe) OR
                 (iOUT_Step = ST185_Pnp_e.DONE);

// Aktueller Schritt			
iOUT_Step := iOUT_Step; //Im Ablauf gesetzt		
sOUT_Step := TO_STRING(iOUT_Step);
//Fehlernummer	 
bOUT_Error := bOUT_Error; //Im Ablauf/FB gesetzt	
(* Warte (alle Schritte wo auf FG oder Teile gewartet wird) *)
xOUT_Wait := (iOUT_Step = ST185_Pnp_e.DONE);

xOUT_MoverFree := iOUT_Step >= ST185_Pnp_e.READY AND
                  NOT (iOUT_Step >= ST185_Pnp_e.MOVER_NEEDED_OP AND iOUT_Step < ST185_Pnp_e.MOVER_FREE_OP);

iOUT_ProcessCompletion := F_GetPercent_2(iOUT_Step,
                                         ST185_Pnp_e.lift_piece_and_down_to_pick,
                                         ST185_Pnp_e.take_out_piece_and_put_down_lifter,
                                         ST185_Pnp_e.hor_move_to_extraction_pipe,
                                         ST185_Pnp_e.hor_to_track_pos);

// TODO Valve should not be set when we are in simulation !
IO_BOX.Q5404_14_ST185_greifer_patrone_links :=
    fb_griperLeft.xOUT_VALVE AND NOT (PNP_SIMULATION AND CONST.USE_SIMULATION);

IO_BOX.Q5405_14_ST185_greifer_patrone_rechts :=
    fb_griperRight.xOUT_VALVE AND NOT (PNP_SIMULATION AND CONST.USE_SIMULATION);

IO_BOX.Q5406_14_ST185_niederhalter_deckel_links :=
    fb_clampLeft.xOUT_VALVE AND NOT (PNP_SIMULATION AND CONST.USE_SIMULATION);

IO_BOX.Q5407_14_ST185_Niederhalter_deckel_rechts :=
    fb_clampRight.xOUT_VALVE AND NOT (PNP_SIMULATION AND CONST.USE_SIMULATION);

// safety pneumatics
IO_BOX.Q5409_14_ST185_Break_axis_02Z := NOT fb_simu_axis185_02Z.pIN_axisHW^.Status.Error AND
                                        _02_SAFETY.xOUT_ESout AND _02_SAFETY.xOUT_MonOut AND
                                        NOT (PNP_SIMULATION AND CONST.USE_SIMULATION);

(*******************************************************************)
(* Ablauf *)
(*******************************************************************)

(* Sperren *)
IF xIN_Sperre THEN
    iPreStep  := ST185_Pnp_e.RESET;
    iOUT_Step := ST185_Pnp_e.RESET;
    RETURN;
END_IF;

(* Schritt Merker *)
IF iAktStep <> iOUT_Step THEN
    iLastStep := iAktStep;
    iAktStep  := iOUT_Step;
END_IF

(* START *)
rtSTART(CLK := xIN_Start);

IF 0 = bOUT_Error THEN
    IF fb_axis185_01Y.xOUT_ErrFlag THEN
        bOUT_Error := 11;
    ELSIF fb_axis185_02Z.xOUT_ErrFlag THEN
        bOUT_Error := 12;
    ELSIF fb_axis185_10Z.xOUT_ErrFlag THEN
        bOUT_Error := 13;
    ELSIF fb_griperLeft.xOUT_ErrFlag THEN
        bOUT_Error := 14;
    ELSIF fb_griperRight.xOUT_ErrFlag THEN
        bOUT_Error := 15;
    ELSIF fb_clampLeft.xOUT_ErrFlag THEN
        bOUT_Error := 16;
    ELSIF fb_clampRight.xOUT_ErrFlag THEN
        bOUT_Error := 17;
    END_IF
ELSIF iOUT_Step < ST185_Pnp_e.ERROR THEN
    iOUT_Step := ST185_Pnp_e.ERROR;
END_IF

(*******************************************************************)
(* Schrittkette  *)
(*******************************************************************)
CASE iOUT_Step OF

(***** RESET *****)
ST185_Pnp_e.RESET: (* Schritt 0-99 *)
    //Initialisierungen
    //Start Init Ablauf
    IF xIN_Init THEN
        iOUT_Step := ST185_Pnp_e.INIT;
        //Start Manu Ablauf
    ELSIF enIN_State = StateMANU AND xIN_NextStep THEN
        iOUT_Step := ST185_Pnp_e.INIT;
    END_IF;

(***** INIT *****)
ST185_Pnp_e.INIT: (* Grundstellung ? *)
    IF fb_axis185_10Z.xOUT_HOMED THEN
        iOUT_Step := ST185_Pnp_e.Init_Vertical_10_Down;
    ELSE
        iOUT_Step := ST185_Pnp_e.Init_HomeVertical_10;
    END_IF

ST185_Pnp_e.Init_HomeVertical_10:
    IF fb_axis185_10Z.xOUT_HOMED THEN
        iOUT_Step := ST185_Pnp_e.Init_Vertical_10_Down;
    END_IF

ST185_Pnp_e.Init_Vertical_10_Down:
    IF fb_PnpVerAxis_10.MoveTo(ST185_PnpHorPos_e.GS).xOUT_InPos THEN
        IF fb_axis185_02Z.xOUT_HOMED THEN
            iOUT_Step := ST185_Pnp_e.Init_Vertical_02_up;
        ELSE
            iOUT_Step := ST185_Pnp_e.Init_HomeVertical_02;
        END_IF
    END_IF

ST185_Pnp_e.Init_HomeVertical_02:
    IF fb_axis185_02Z.xOUT_HOMED THEN
        iOUT_Step := ST185_Pnp_e.Init_Vertical_02_up;
    END_IF

ST185_Pnp_e.Init_Vertical_02_up:
    IF fb_griperLeft.xIN_GS OR fb_griperRight.xIN_GS THEN
        IF fb_PnpVerAxis_02.MoveTo(ST185_PnpVerPos_e.up_full).xOUT_InPos THEN
            IF fb_axis185_01Y.xOUT_HOMED THEN
                iOUT_Step := ST185_Pnp_e.Init_HorizontalToTrack;
            ELSE
                iOUT_Step := ST185_Pnp_e.Init_HomeHorizontal;
            END_IF
        END_IF
    ELSE
        IF fb_PnpVerAxis_02.MoveTo(ST185_PnpVerPos_e.up_empty).xOUT_InPos THEN
            IF fb_axis185_01Y.xOUT_HOMED THEN
                iOUT_Step := ST185_Pnp_e.Init_HorizontalToTrack;
            ELSE
                iOUT_Step := ST185_Pnp_e.Init_HomeHorizontal;
            END_IF
        END_IF
    END_IF

ST185_Pnp_e.Init_HomeHorizontal:
    IF fb_axis185_01Y.xOUT_HOMED THEN
        iOUT_Step := ST185_Pnp_e.Init_HorizontalToTrack;
    END_IF

ST185_Pnp_e.Init_HorizontalToTrack:
    IF fb_PnpHorAxis.MoveTo(ST185_PnpHorPos_e.GS).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.Init_check_if_pces_handled;
    END_IF

ST185_Pnp_e.Init_check_if_pces_handled:
    IF (NOT _20_AUTO.recipe.st185.xUseVerLeft AND fb_griperLeft.xOUT_inGS) OR
       (NOT _20_AUTO.recipe.st185.xUseVerRight AND fb_griperRight.xOUT_inGS) THEN
        // ERROR: a piece is present in an unused gripper
        bOUT_Error := 1;
        iOUT_Step  := ST185_Pnp_e.ERROR;

    ELSIF _20_AUTO.recipe.st185.xUseVerLeft AND
          NOT fb_griperLeft.xOUT_inGS AND
          _20_AUTO.recipe.st185.xUseVerRight AND
          fb_griperRight.xOUT_inGS THEN
        // ERROR:  Initialisation failed. A piece is missing in the left griper.
        bOUT_Error := 2;
        iOUT_Step  := ST185_Pnp_e.ERROR;

    ELSIF _20_AUTO.recipe.st185.xUseVerLeft AND
          fb_griperLeft.xOUT_inGS AND
          _20_AUTO.recipe.st185.xUseVerRight AND
          NOT fb_griperRight.xOUT_inGS THEN
        // ERROR:  Initialisation failed. A piece is missing in the right griper.
        bOUT_Error := 3;
        iOUT_Step  := ST185_Pnp_e.ERROR;

    ELSIF NOT fb_griperLeft.xOUT_inGS AND NOT fb_griperRight.xOUT_inGS THEN
        iOUT_Step := ST185_Pnp_e.READY;

    ELSE
        iOUT_Step := ST185_Pnp_e.hor_move_to_extraction_pipe;
    END_IF

(***** READY / INIT DONE *****)
ST185_Pnp_e.READY:    (* Start AUTO:END:LEER:STEP *)
    IF xIN_Start THEN //IF rtSTART.Q THEN
        iOUT_Step := ST185_Pnp_e.START;
    END_IF

(***** START  ZYKLISCH *****)
ST185_Pnp_e.START: (*  *)
    IF xIN_NextStep AND pxFG_Freigabe^ THEN
        iOUT_Step := ST185_Pnp_e.waitOnMover;
    END_IF;

ST185_Pnp_e.waitOnMover:
    IF xIN_NextStep AND __ISVALIDREF(Mover) OR HMI.bArForceCyclusState[StationList_e.ST185] THEN
        iOUT_Step := ST185_Pnp_e.lift_piece_and_down_to_pick;
    END_IF

ST185_Pnp_e.lift_piece_and_down_to_pick:
    IF xIN_NextStep AND
       fb_PnpVerAxis_02.MoveTo(ST185_PnpVerPos_e.down_empty).xOUT_InPos AND
       fb_PnpVerAxis_10.MoveTo(ST185_PnpHorPos_e.AS).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.close_griper_pick_piece;
    END_IF

ST185_Pnp_e.close_griper_pick_piece:
    IF xIN_NextStep AND
       (NOT _20_AUTO.recipe.st185.xUseVerLeft OR fb_griperLeft.xOUT_inGS) AND
       (NOT _20_AUTO.recipe.st185.xUseVerRight OR fb_griperRight.xOUT_inGS) THEN
        iOUT_Step := ST185_Pnp_e.clamp_down_maintain_piece;
    END_IF

ST185_Pnp_e.clamp_down_maintain_piece:
    IF xIN_NextStep AND
       (NOT _20_AUTO.recipe.st185.xUseVerLeft OR fb_clampLeft.xOUT_inAS) AND
       (NOT _20_AUTO.recipe.st185.xUseVerRight OR fb_clampRight.xOUT_inAS) THEN
        iOUT_Step := ST185_Pnp_e.take_out_piece_and_put_down_lifter;
    END_IF

ST185_Pnp_e.take_out_piece_and_put_down_lifter:
    IF xIN_NextStep AND
       fb_PnpVerAxis_02.MoveTo(ST185_PnpVerPos_e.up_full).xOUT_InPos AND
       fb_PnpVerAxis_10.MoveTo(ST185_PnpHorPos_e.GS).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.SYNC_MOVER_LEAVE;

        IF __ISVALIDREF(Mover) THEN
            Mover.UnloadPieces();
        END_IF
    END_IF

ST185_Pnp_e.SYNC_MOVER_LEAVE:
    // The mover needs to be taken by MoverMan
    IF NOT __ISVALIDREF(Mover) THEN
        iOUT_Step := ST185_Pnp_e.hor_move_to_extraction_pipe;
    END_IF

ST185_Pnp_e.hor_move_to_extraction_pipe:
    IF xIN_NextStep AND fb_PnpHorAxis.MoveTo(ST185_PnpHorPos_e.AS).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.ver_down_in_pipe;
    END_IF

ST185_Pnp_e.ver_down_in_pipe:
    IF xIN_NextStep AND fb_PnpVerAxis_02.MoveTo(ST185_PnpVerPos_e.down_full).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.clamp_up_to_rest_position;
    END_IF

ST185_Pnp_e.clamp_up_to_rest_position:
    IF xIN_NextStep AND
       (NOT _20_AUTO.recipe.st185.xUseVerLeft OR fb_clampLeft.xOUT_inGS) AND
       (NOT _20_AUTO.recipe.st185.xUseVerRight OR fb_clampRight.xOUT_inGS) THEN
        iOUT_Step := ST185_Pnp_e.open_griper_put_piece;
    END_IF

ST185_Pnp_e.open_griper_put_piece:
    IF xIN_NextStep AND
       (NOT _20_AUTO.recipe.st185.xUseVerLeft OR fb_griperLeft.xOUT_inAS) AND
       (NOT _20_AUTO.recipe.st185.xUseVerRight OR fb_griperRight.xOUT_inAS) THEN
        iOUT_Step := ST185_Pnp_e.ver_up_empty;
    END_IF

ST185_Pnp_e.ver_up_empty:
    IF xIN_NextStep AND fb_PnpVerAxis_02.MoveTo(ST185_PnpVerPos_e.up_empty).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.hor_to_track_pos;
    END_IF

ST185_Pnp_e.hor_to_track_pos:
    IF xIN_NextStep AND fb_PnpHorAxis.MoveTo(ST185_PnpHorPos_e.GS).xOUT_InPos THEN
        iOUT_Step := ST185_Pnp_e.DONE;
    END_IF

(***** DONE *****)
ST185_Pnp_e.DONE: (* Letzter Schritt  *)
    IF NOT xIN_AlarmAktiv THEN
        //AUTO
        IF (enIN_State = StateAUTO) AND xIN_RestartCYCL THEN
            iOUT_Step := ST185_Pnp_e.START;
            //STEP / END / LEERFAHREN restart
        ELSIF (enIN_State < StateAUTO) AND (enIN_State >= enState.StateSTEP) AND xIN_RestartCYCL THEN
            iOUT_Step := ST185_Pnp_e.START;
            //STEP / END / LEERFAHREN fertig
        ELSIF (enIN_State = StateREADY) THEN
            iOUT_Step := ST185_Pnp_e.READY;
        END_IF;
    END_IF;

(************ ERROR *************)
ST185_Pnp_e.ERROR: (* ERROR *) ;

END_CASE;
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>